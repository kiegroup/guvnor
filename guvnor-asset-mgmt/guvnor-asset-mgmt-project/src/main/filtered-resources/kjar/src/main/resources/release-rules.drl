/*
 * Copyright 2015 Red Hat, Inc. and/or its affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * 
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
*/

package defaultPackge


import org.kie.api.runtime.process.WorkflowProcessInstance;
import org.slf4j.Logger;


global Logger logger;

rule "Project URI is not set" ruleflow-group "release-rules"
    when
        $processInstance : WorkflowProcessInstance(processId == "guvnor-asset-management.ReleaseProject")
        String(this.isEmpty()) from $processInstance.getVariable("ProjectURI");
    then
        String reason = "Not valid for release as project uri is not given " + $processInstance.getVariable("ProjectURI");
        logger.error(reason);
        $processInstance.setVariable("ValidForRelease", Boolean.FALSE);
        $processInstance.setVariable("Exception", reason);

end

rule "Release branch and version is not set" ruleflow-group "release-rules"
    when
        $processInstance : WorkflowProcessInstance(processId == "guvnor-asset-management.ReleaseProject")
        String(this.isEmpty()) from $processInstance.getVariable("ToReleaseBranch");
        String(this.isEmpty()) from $processInstance.getVariable("ToReleaseVersion");
    then
        String reason = "Not valid for release as branch [1] and/or version [2] is not given "
                               + $processInstance.getVariable("ToReleaseBranch") + ", " + $processInstance.getVariable("ToReleaseVersion");
        logger.error(reason);
        $processInstance.setVariable("ValidForRelease", Boolean.FALSE);
        $processInstance.setVariable("Exception", reason);

end

rule "Release branch does not start with release" ruleflow-group "release-rules"
    when
        $processInstance : WorkflowProcessInstance(processId == "guvnor-asset-management.ReleaseProject")
        String(!this.startsWith('release')) from $processInstance.getVariable("ToReleaseBranch");
    then
        String reason = "Not valid for release as branch to release from does not start with release - given name is "
                                + $processInstance.getVariable("ToReleaseBranch");
        logger.error(reason);
        $processInstance.setVariable("ValidForRelease", Boolean.FALSE);
        $processInstance.setVariable("Exception", reason);

end

rule "Check deployment to runtime details" ruleflow-group "release-rules"
    when
        $processInstance : WorkflowProcessInstance(processId == "guvnor-asset-management.ReleaseProject")
        Boolean(this == true) from $processInstance.getVariable("DeployToRuntime");
        String(this.isEmpty()) from $processInstance.getVariable("Username");
        String(this.isEmpty()) from $processInstance.getVariable("Password");
        String(this.isEmpty()) from $processInstance.getVariable("ExecServerURL");
    then
        String reason = "Not valid for release as deploy to runtime was selected but no details provided "
                               + $processInstance.getVariable("Username") + ", "+ $processInstance.getVariable("Password")
                                + ", " + $processInstance.getVariable("ExecServerURL");
        logger.error(reason);
        $processInstance.setVariable("ValidForRelease", Boolean.FALSE);
        $processInstance.setVariable("Exception", reason);

end

rule "Release version ends with SNAPSHOT" ruleflow-group "release-rules"
    when
        $processInstance : WorkflowProcessInstance(processId == "guvnor-asset-management.ReleaseProject")
        String(this.endsWith('-SNAPSHOT')) from $processInstance.getVariable("ToReleaseVersion");
    then
        String reason = "Not valid for release as version for release is snapshot - given name is "
                                + $processInstance.getVariable("ToReleaseVersion");
        logger.error(reason);
        $processInstance.setVariable("ValidForRelease", Boolean.FALSE);
        $processInstance.setVariable("Exception", reason);

end